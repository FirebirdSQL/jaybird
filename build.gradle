import org.apache.tools.ant.filters.*
import org.asciidoctor.gradle.jvm.AsciidoctorTask

/*
  Gradle build script for Jaybird - Firebird JDBC driver.

  Define target Java version:
  - -PbuildProfile=java8           : Java 8  (supported)
  - -PbuildProfile=java11or
    not specified or unknown value : Java 11 (supported)
  - -PbuildProfile=java17          : Java 17 (for compatibility testing only)

  Uploading archives:

  publish -PcredentialsPassphrase=<credentials password>
 */

plugins {
    id 'java-library'
    id 'nu.studer.credentials' version '2.1'
    id 'maven-publish'
    id 'signing'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

defaultTasks 'clean', 'build'

apply from: 'build-properties.gradle'

group = 'org.firebirdsql.jdbc'
version = project.'version.maven'

allprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Test) {
        systemProperty 'file.encoding', 'UTF-8'
    }
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
    registerFeature('native') {
        usingSourceSet(sourceSets.main)
    }
}

ext {
    junit_jupiter = '5.7.1'
    hamcrest = '2.2'
    assertj = '3.19.0'
    jmock = '2.12.0'
    mockito = '3.11.2'
}

dependencies {
    implementation 'org.antlr:antlr4-runtime:4.7.2'
    nativeImplementation 'net.java.dev.jna:jna:5.5.0'

    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'

    // Use JUnit Jupiter API for testing.
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_jupiter"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter"

    // Support JUnit 4
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit_jupiter"
    testImplementation 'junit:junit:4.13.2'

    testImplementation "org.hamcrest:hamcrest-core:$hamcrest"
    testImplementation "org.hamcrest:hamcrest-library:$hamcrest"
    testImplementation "org.hamcrest:hamcrest-core:$hamcrest"
    testImplementation "org.assertj:assertj-core:$assertj"

    testImplementation("org.jmock:jmock-junit4:$jmock") {
        exclude group: 'org.jmock', module: 'jmock-legacy'
    }
    testImplementation("org.jmock:jmock-imposters:$jmock")
    testImplementation 'net.bytebuddy:byte-buddy:1.10.22'

    testImplementation "org.mockito:mockito-core:$mockito"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockito"

    testImplementation 'org.ow2.asm:asm:9.1'
    testImplementation 'org.objenesis:objenesis:3.2'
}

sourceSets {
    main {
        java {
            srcDirs = findProperty('buildProfile') == 'java8'
                    ? ['src/main', 'src/extern', 'src/jdbc_42', 'src/jna-client', 'src/openoffice']
                    : ['src/main', 'src/extern', 'src/jdbc_42', 'src/jna-client', 'src/openoffice', 'src/chacha_j11']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test', 'src/test_42', 'src/jna-test']
        }
        resources {
            srcDirs = ['src/test_resources']
        }
    }
    tools {
        java {
            srcDirs = ['src/messages']
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

processResources {
    filter ReplaceTokens, tokens: [
            'NAME'         : project.capitalizedName,
            'VERSION'      : project.'version.simple',
            'MAVEN_NAME'   : project.name,
            'VERSION_FULL' : project.'version.maven',
            'VERSION_MAJOR': project.'version.major',
            'VERSION_MINOR': project.'version.minor'
    ]
}

//apply from: 'documentation.gradle'

asciidoctorj {
    attributes 'revnumber': null,
            'version_simple': project.'version.simple',
            'version_wo_target': "${project.'version.simple'}${project.'version.tag'}",
            'version_tag': project.'version.tag',
            'version_example': "${project.'version.simple'}.java8${project.'version.tag'}",
            'stylesdir': file('src/docs/theme/jaybird-html'),
            'stylesheet': 'firebird.css',
            'docinfo': 'shared',
            'docinfodir': file('src/docs/theme/jaybird-html/docinfo')
}

task dist(type: Zip) {
    dependsOn assemble, asciidoctor
    destinationDirectory = file("$buildDir/dist")

    from jar.outputs
    from javadocJar.outputs
    from sourcesJar.outputs
    from(asciidoctor.outputs) {
        include 'release_notes.html'
    }
    from(asciidoctor.outputs) {
        exclude 'release_notes.html'
        into 'docs'
    }
    from(javadoc.outputs) {
        into 'docs/api'
    }
    from('.') {
        include 'CONTRIBUTING.md'
    }
    from(configurations.runtimeClasspath) {
        into 'lib'
    }
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Specification-Title': project.'specification.title',
                'Specification-Version': project.'specification.version',
                'Specification-Vendor': project.'specification.vendor',
                'Implementation-Title': project.'implementation.title',
                'Implementation-Url': project.'implementation.url',
                'Implementation-Version': "$project.version (build: variant=$project.name tag=${project.'version.svntag'} date=${project.'build.id'})",
                'Implementation-Vendor': project.'implementation.vendor',
                'Implementation-Vendor-Id': project.'implementation.vendor.id',
                'Automatic-Module-Name': 'org.firebirdsql.jaybird'
        )
    }
}

javadoc {
    options.author()
    options.windowTitle = "$project.capitalizedName API"
    options.docTitle = project.capitalizedName
    options.bottom = "Copyright &copy; 2001-${project.YEAR} Jaybird (Firebird JDBC) team. All rights reserved."
    def currentJavaVersion = JavaVersion.current()
    if (currentJavaVersion.isJava9Compatible()) {
        options.addBooleanOption('html5', true)

        if (currentJavaVersion.isJava11Compatible() && !currentJavaVersion.isCompatibleWith(JavaVersion.VERSION_13)) {
            // Ensure search index works correctly
            options.addBooleanOption('-no-module-directories', true)
        }
    }
    options.addBooleanOption('Xdoclint:none', true)
    exclude 'org/firebirdsql/jdbc/oo/**'
}

test {
    println "Running tests for type ${project.'test.gds_type'}"
    // Use junit platform for unit tests
    useJUnitPlatform()

    // Test configuration, defaults specified in gradle.properties (modify through ext, or use -P<prop>=<value>)
    systemProperties(
            'test.user': project.'test.user',
            'test.password': project.'test.password',
            'test.db.dir': project.'test.db.dir',
            'test.db.host': project.'test.db.host',
            'test.db.port': project.'test.db.port',
            'test.db.lc_ctype': project.'test.db.lc_ctype',
            'test.gds_type': project.'test.gds_type',
            'test.use_firebird_autocommit': project.'test.use.firebird.autocommit',
            'jdk.net.useFastTcpLoopback': project.findProperty('jdk.net.useFastTcpLoopback') ?: 'false'
    )
    if (project.hasProperty('test.jna.library.path')) {
        systemProperty 'jna.library.path', project.'test.jna.library.path'
    } else if (project.'test.gds_type' == 'NATIVE' || project.'test.gds_type' == 'EMBEDDED') {
        println "Running test type ${project.'test.gds_type'} without explicit native library path. " +
                "Specify property 'test.jna.library.path' to point to a Firebird client location (NATIVE) or " +
                "Firebird install (EMBEDDED)."
    }

    doFirst {
        // ensure the 'standard' database directory exists before the test runs
        def standardDbDir = file("$buildDir/tmp/db")
        mkdir standardDbDir
        // ensures a Firebird server can create, use and drop databases from this directory
        ant.chmod(dir: standardDbDir, perm: '777')
    }
}

apply from: 'publish.gradle'
