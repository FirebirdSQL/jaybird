import org.apache.tools.ant.filters.*
import org.asciidoctor.gradle.jvm.AsciidoctorTask

/*
  Gradle build script for Jaybird - Firebird JDBC driver.

  Define target Java version:
  - -PbuildProfile=java8           : Java 8  (supported)
  - -PbuildProfile=java11 or
    not specified or unknown value : Java 11 (supported)
  - -PbuildProfile=java17          : Java 17 (for compatibility testing only)

  Uploading archives:

  publish -PcredentialsPassphrase=<credentials password>
 */

plugins {
    id 'java-library'
    id 'nu.studer.credentials' version '3.0'
    id 'maven-publish'
    id 'signing'
    id 'org.asciidoctor.jvm.convert' version '4.0.4'
}

defaultTasks 'clean', 'build'

apply from: 'build-properties.gradle'

group = 'org.firebirdsql.jdbc'
version = project.'version.maven'

allprojects {
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Test).configureEach {
        systemProperty 'file.encoding', 'UTF-8'
    }
}

repositories {
    mavenCentral()
}

base {
    archivesName = project.mavenName
}

java {
    def javaVersion = JavaVersion.VERSION_11
    if (project.'maven.jdkversion' == 'java17') {
        javaVersion = JavaVersion.VERSION_17
    } else if (project.'maven.jdkversion' == 'java8') {
        javaVersion = JavaVersion.VERSION_1_8
    }
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    withJavadocJar()
    withSourcesJar()
    registerFeature('native') {
        usingSourceSet(sourceSets.main)
    }
}

ext {
    junit_jupiter = '5.12.1'
    hamcrest = '3.0'
    assertj = '3.27.3'
    // Mockito 5 requires Java 11 or higher
    mockito = findProperty('buildProfile') == 'java8' ? '4.11.0' : '5.16.1'
}

dependencies {
    nativeImplementation 'net.java.dev.jna:jna:5.17.0'

    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
    compileOnly 'jakarta.servlet:jakarta.servlet-api:5.0.0'

    // Use JUnit Jupiter API for testing.
    testImplementation platform("org.junit:junit-bom:$junit_jupiter")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation "org.hamcrest:hamcrest-core:$hamcrest"
    testImplementation "org.hamcrest:hamcrest-library:$hamcrest"
    testImplementation "org.hamcrest:hamcrest-core:$hamcrest"
    testImplementation 'com.spotify:hamcrest-optional:1.3.2'
    testImplementation "org.assertj:assertj-core:$assertj"

    testImplementation "org.mockito:mockito-core:$mockito"
    if (findProperty('buildProfile') == 'java8') {
        testImplementation "org.mockito:mockito-inline:$mockito"
    }
    testImplementation "org.mockito:mockito-junit-jupiter:$mockito"
}

sourceSets {
    main {
        java {
            srcDirs = findProperty('buildProfile') == 'java8'
                    ? ['src/main', 'src/extern', 'src/jna-client', 'src/openoffice']
                    : ['src/main', 'src/extern', 'src/jna-client', 'src/openoffice', 'src/chacha_j11']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test', 'src/jna-test']
        }
        resources {
            srcDirs = ['src/test_resources']
        }
    }
    tools {
        java {
            srcDirs = ['src/messages']
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

processResources {
    filter ReplaceTokens, tokens: [
            'NAME'         : project.capitalizedName,
            'VERSION'      : project.'version.simple',
            'MAVEN_NAME'   : project.mavenName,
            'VERSION_FULL' : project.'version.maven',
            'VERSION_MAJOR': project.'version.major',
            'VERSION_MINOR': project.'version.minor'
    ]
}

//apply from: 'documentation.gradle'

asciidoctorj {
    // NOTE: Don't upgrade to v 3.0.0, as it requires Java 11
    version = '2.5.13'

    docExtensions file('doc-extension.groovy')
}

tasks.named('asciidoctor', AsciidoctorTask).configure {
    executionMode = OUT_OF_PROCESS
    attributes 'version_simple': project.'version.simple',
            'version_wo_target': "${project.'version.simple'}${project.'version.tag'}",
            'version_tag': project.'version.tag',
            'version_example': "${project.'version.simple'}.java11${project.'version.tag'}",
            'stylesdir': file('src/docs/theme/jaybird-html'),
            'stylesheet': 'firebird.css',
            'docinfo': 'shared',
            'docinfodir': file('src/docs/theme/jaybird-html/docinfo'),
            'revnumber': false

    if (JavaVersion.current().isJava9Compatible()) {
        jvm {
            jvmArgs "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED"
        }
    }
}

tasks.register('dist', Zip) {
    destinationDirectory = layout.buildDirectory.dir("dist")

    from jar.outputs
    from javadocJar.outputs
    from sourcesJar.outputs
    from(asciidoctor.outputs) {
        include 'release_notes.html'
    }
    from(asciidoctor.outputs) {
        exclude 'release_notes.html'
        into 'docs'
    }
    from(javadoc.outputs) {
        into 'docs/api'
    }
    from('.') {
        include 'CONTRIBUTING.md'
        include 'SECURITY.md'
    }
    from(configurations.runtimeClasspath) {
        into 'lib'
    }
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Specification-Title': project.'specification.title',
                'Specification-Version': project.'specification.version',
                'Specification-Vendor': project.'specification.vendor',
                'Implementation-Title': project.'implementation.title',
                'Implementation-Url': project.'implementation.url',
                'Implementation-Version': "$project.version (build: variant=$project.mavenName tag=${project.'version.svntag'} date=${project.'build.id'})",
                'Implementation-Vendor': project.'implementation.vendor',
                'Implementation-Vendor-Id': project.'implementation.vendor.id',
                'Automatic-Module-Name': 'org.firebirdsql.jaybird'
        )
    }
}

javadoc {
    options.author()
    options.windowTitle = "$project.capitalizedName ${project.'version.maven'} API"
    options.docTitle = "$project.capitalizedName ${project.'version.maven'}"
    options.bottom = "Copyright &copy; 2001-${project.YEAR} Jaybird (Firebird JDBC) team. All rights reserved."
    def currentJavaVersion = JavaVersion.current()
    if (currentJavaVersion.isJava9Compatible()) {
        options.addBooleanOption('html5', true)

        if (currentJavaVersion.isJava11Compatible() && !currentJavaVersion.isCompatibleWith(JavaVersion.VERSION_13)) {
            // Ensure search index works correctly
            options.addBooleanOption('-no-module-directories', true)
        }
    }
    options.addBooleanOption('Xdoclint:none', true)
    exclude 'org/firebirdsql/jdbc/oo/**'
}

test {
    println "Running tests for type ${project.'test.gds_type'}"
    // Use junit platform for unit tests
    useJUnitPlatform()

    // Test configuration, defaults specified in gradle.properties (modify through ext, or use -P<prop>=<value>)
    systemProperties(
            'test.user': project.'test.user',
            'test.password': project.'test.password',
            'test.db.dir': project.'test.db.dir',
            'test.db.host': project.'test.db.host',
            'test.db.port': project.'test.db.port',
            'test.db.lc_ctype': project.'test.db.lc_ctype',
            'test.gds_type': project.'test.gds_type',
            'test.use_firebird_autocommit': project.'test.use.firebird.autocommit',
            'jdk.net.useFastTcpLoopback': project.findProperty('jdk.net.useFastTcpLoopback') ?: 'false',
            'test.db_on_docker': project.findProperty('test.dbondocker') ?: 'false',
    )
    if (project.hasProperty('test.jna.library.path')) {
        systemProperty 'jna.library.path', project.'test.jna.library.path'
    } else if (project.'test.gds_type' == 'NATIVE' || project.'test.gds_type' == 'EMBEDDED') {
        println "Running test type ${project.'test.gds_type'} without explicit native library path. " +
                "Specify property 'test.jna.library.path' to point to a Firebird client location (NATIVE) or " +
                "Firebird install (EMBEDDED)."
    }

    doFirst {
        // ensure the 'standard' database directory exists before the test runs
        def standardDbDir = file(layout.buildDirectory.dir("tmp/db"))
        mkdir standardDbDir
        // ensures a Firebird server can create, use and drop databases from this directory
        ant.chmod(dir: standardDbDir, perm: '777')
    }
}

apply from: 'publish.gradle'
